#!python
import os, sys, subprocess

# Try to detect the host platform automatically.
# This is used if no `platform` argument is passed
if sys.platform.startswith('linux'):
    host_platform = 'linux'
elif sys.platform == 'darwin':
    host_platform = 'osx'
elif sys.platform == 'win32' or sys.platform == 'msys':
    host_platform = 'windows'
else:
    raise ValueError(
        'Could not detect platform automatically, please specify with '
        'platform=<platform>'
    )

opts = Variables([], ARGUMENTS)

# Gets the standard flags CC, CCX, etc.
env = Environment(ENV = os.environ)


# Define our options
opts.Add(EnumVariable('target', "Compilation target", 'debug', ['d', 'debug', 'r', 'release']))
opts.Add(EnumVariable('platform', "Compilation platform", '', ['', 'windows', 'osx']))
opts.Add(EnumVariable('p', "Compilation target, alias for 'platform'", '', ['', 'windows', 'osx']))
opts.Add(BoolVariable('use_llvm', "Use the LLVM / Clang compiler", 'no'))
opts.Add(PathVariable('target_path', 'The path where the lib is installed.', '../wwise-gdnative/gdnative-demo/addons/wwise/bin/', PathVariable.PathIsDirCreate))
opts.Add(PathVariable('target_name', 'The library name.', 'WaapiGDNative', PathVariable.PathAccept))
opts.Add(PathVariable('wwise_sdk', 'The Wwise SDK path', '', PathVariable.PathAccept))
opts.Add(EnumVariable('macos_arch', 'macOS architecture',
         'universal', ['universal', 'x86_64', 'arm64']))

# Local dependency paths, adapt them to your setup
godot_headers_path = "../godot-cpp/godot-headers/"
cpp_bindings_path = "../godot-cpp/"
cpp_library = "libgodot-cpp"

# only support 64 at this time..
bits = 64

# Updates the environment with the option variables.
opts.Update(env)

if host_platform == 'windows':
    if bits == 64:
        env = Environment(TARGET_ARCH='amd64')
    opts.Update(env)

# Wwise dependency paths
wwise_sdk_headers_path = env['wwise_sdk'] + "/include/"
wwise_sdk_libs_path = ""

if env['platform'] == "windows":
    if env['target'] in ('debug', 'd'):
        wwise_sdk_libs_path = env['wwise_sdk'] + "/x64_vc160/Debug/lib/"
    else:
        wwise_sdk_libs_path = env['wwise_sdk'] + "/x64_vc160/Release/lib/"

if env['platform'] == "osx":
    if env['target'] in ('debug', 'd'):
        wwise_sdk_libs_path = env['wwise_sdk'] + "/Mac/Debug/lib/"
    else:
        wwise_sdk_libs_path = env['wwise_sdk'] + "/Mac/Release/lib/"

print("Wwise SDK headers path: " + wwise_sdk_headers_path)
print("Wwise SDK libs path: " + wwise_sdk_libs_path)

# Process some arguments
if env['use_llvm']:
    env['CC'] = 'clang'
    env['CXX'] = 'clang++'

if env['p'] != '':
    env['platform'] = env['p']

if env['platform'] == '':
    print("No valid target platform selected.")
    quit();

# For the reference:
# - CCFLAGS are compilation flags shared between C and C++
# - CFLAGS are for C-specific compilation flags
# - CXXFLAGS are for C++-specific compilation flags
# - CPPFLAGS are for pre-processor flags
# - CPPDEFINES are for pre-processor defines
# - LINKFLAGS are for linking flags

# Check our platform specifics
if env['platform'] == "osx":
    env['target_path'] += 'osx/'
    if env['target'] in ('debug', 'd'):
        env['target_path'] += 'debug/'
    else:
        env['target_path'] += 'release/'

    cpp_library += '.osx'

    if env["macos_arch"] == "universal":
        env.Append(LINKFLAGS=["-arch", "x86_64", "-arch", "arm64"])
        env.Append(CCFLAGS=["-arch", "x86_64", "-arch", "arm64"])
    else:
        env.Append(LINKFLAGS=["-arch", env["macos_arch"]])
        env.Append(CCFLAGS=["-arch", env["macos_arch"]])
    
    if env['target'] in ('debug', 'd'):
        env.Append(CPPDEFINES=['AK_ENABLE_ASSERTS'])
        env.Append(CCFLAGS=['-g', '-O2'])
        env.Append(CXXFLAGS=['-std=c++17'])
        env.Append(LINKFLAGS=['-framework', 'CoreAudio', '-Wl,-undefined,dynamic_lookup'])
    else:
        env.Append(CPPDEFINES=['AK_ENABLE_ASSERTS'])        
        env.Append(CPPDEFINES=['AK_OPTIMIZED'])
        env.Append(CCFLAGS=['-g', '-O3'])
        env.Append(CXXFLAGS=['-std=c++17'])
        env.Append(LINKFLAGS=['-framework', 'CoreAudio', '-Wl,-undefined,dynamic_lookup'])

elif env['platform'] == "windows":
    env['target_path'] += 'win64/'
    if env['target'] in ('debug', 'd'):
        env['target_path'] += 'debug/'
    else:
        env['target_path'] += 'release/'

    cpp_library += '.windows'
    
    env.Append(CPPDEFINES=['WIN32', '_WIN32', '_WINDOWS', '_CRT_SECURE_NO_WARNINGS', 'UNICODE'])
    env.Append(CCFLAGS=['-W3', '-GR'])
    #env.Append(CCFLAGS=['/GS', '/GL', '/Gy'])

    if env['target'] in ('debug', 'd'):
        env.Append(CPPDEFINES=['_DEBUG'])
        env.Append(CCFLAGS=['-EHsc', '-MDd', '-ZI'])
        env.Append(LINKFLAGS=['-DEBUG'])
    else:
        env.Append(CPPDEFINES=['AK_OPTIMIZED'])
        env.Append(CPPDEFINES=['NDEBUG'])
        env.Append(CCFLAGS=['-O2', '-EHsc', '-MD'])

if env['target'] in ('debug', 'd'):
    cpp_library += '.debug'
else:
    cpp_library += '.release'


if env["platform"] == "osx":
    if env['macos_arch'] != 'universal':
        cpp_library += '.' + env['macos_arch']
    else:
        cpp_library += '.' + str(bits)
else:
    cpp_library += '.' + str(bits)

# make sure our binding library is properly includes
env.Append(CPPPATH=['.', godot_headers_path, cpp_bindings_path + 'include/', cpp_bindings_path + 'include/core/', cpp_bindings_path + 'include/gen/'])
env.Append(LIBPATH=[cpp_bindings_path + 'bin/'])
env.Append(LIBS=[cpp_library])

# tweak this if you want to use different folders, or more folders, to store your source code in.
env.Append(CPPPATH=['src/'])

sources = []
sources.append(Glob('src/*.cpp')) 

# wwise specific configuration
env.Append(CPPPATH=[wwise_sdk_headers_path])
env.Append(LIBPATH=[wwise_sdk_libs_path])
env.Append(LIBS=["AkAutobahn"])

library = env.SharedLibrary(target=env['target_path'] + env['target_name'] , source=sources)

Default(library)

# Generates help for the -h scons option.
Help(opts.GenerateHelpText(env))
