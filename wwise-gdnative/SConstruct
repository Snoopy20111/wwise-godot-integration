#!python
import os, sys, subprocess

if sys.version_info < (3,):
    def decode_utf8(x):
        return x
else:
    import codecs
    def decode_utf8(x):
        return codecs.utf_8_decode(x)[0]

# Workaround for MinGW. See:
# http://www.scons.org/wiki/LongCmdLinesOnWin32
if os.name == "nt":
    import subprocess

    def mySubProcess(cmdline,env):
        #print "SPAWNED : " + cmdline
        startupinfo = subprocess.STARTUPINFO()
        startupinfo.dwFlags |= subprocess.STARTF_USESHOWWINDOW
        proc = subprocess.Popen(cmdline, stdin=subprocess.PIPE, stdout=subprocess.PIPE,
            stderr=subprocess.PIPE, startupinfo=startupinfo, shell = False, env = env)
        data, err = proc.communicate()
        rv = proc.wait()
        if rv:
            print("=====")
            print(err.decode("utf-8"))
            print("=====")
        return rv

    def mySpawn(sh, escape, cmd, args, env):

        newargs = ' '.join(args[1:])
        cmdline = cmd + " " + newargs

        rv=0
        if len(cmdline) > 32000 and cmd.endswith("ar") :
            cmdline = cmd + " " + args[1] + " " + args[2] + " "
            for i in range(3,len(args)) :
                rv = mySubProcess( cmdline + args[i], env )
                if rv :
                    break
        else:
            rv = mySubProcess( cmdline, env )

        return rv

def sys_exec(args):
    proc = subprocess.Popen(args, stdout=subprocess.PIPE, text=True)
    (out, err) = proc.communicate()
    return out.rstrip("\r\n").lstrip()

# Try to detect the host platform automatically.
# This is used if no `platform` argument is passed
if sys.platform.startswith('linux'):
    host_platform = 'linux'
elif sys.platform == 'darwin':
    host_platform = 'osx'
elif sys.platform == 'win32' or sys.platform == 'msys':
    host_platform = 'windows'
else:
    raise ValueError(
        'Could not detect platform automatically, please specify with '
        'platform=<platform>'
    )

opts = Variables([], ARGUMENTS)

# Gets the standard flags CC, CCX, etc.
env = Environment(ENV = os.environ)

# Define our options
opts.Add(EnumVariable('target', "Compilation target", 'debug', ['d', 'debug', 'r', 'release']))
opts.Add(EnumVariable('platform', "Compilation platform", '', ['', 'windows', 'x11', 'linux', 'osx', 'ios']))
opts.Add(EnumVariable('p', "Compilation target, alias for 'platform'", '', ['', 'windows', 'x11', 'linux', 'osx']))
opts.Add(BoolVariable('use_llvm', "Use the LLVM / Clang compiler", 'no'))
opts.Add(PathVariable('target_path', 'The path where the lib is installed.', 'gdnative-demo/wwise/bin/', PathVariable.PathIsDirCreate))
opts.Add(PathVariable('target_name', 'The library name.', 'WwiseGDNative', PathVariable.PathAccept))
opts.Add(PathVariable('wwise_sdk', 'The Wwise SDK path', '', PathVariable.PathAccept))
opts.Add(ListVariable('plugins', 'List of plugins', '', ['reflect', 'motion', 'convolution', 'soundseed_grain', 'soundseed_air_impact', 'impacter', 'mastering_suite']))
opts.Add(EnumVariable('ios_arch', 'Target iOS architecture', 'arm64', ['armv7', 'arm64', 'x86_64']))
opts.Add('IPHONEPATH', 'Path to iPhone toolchain', '/Applications/Xcode.app/Contents/Developer/Toolchains/XcodeDefault.xctoolchain')
opts.Add(BoolVariable('ios_simulator', 'Target iOS Simulator', False))

# Local dependency paths, adapt them to your setup
godot_headers_path = "../godot-cpp/godot-headers/"
cpp_bindings_path = "../godot-cpp/"
cpp_library = "libgodot-cpp"

# only support 64 at this time..
bits = 64

# Updates the environment with the option variables.
opts.Update(env)

if host_platform == 'windows':
    if bits == 64:
        env = Environment(TARGET_ARCH='amd64')
    opts.Update(env)

# Wwise dependency paths
wwise_sdk_headers_path = env['wwise_sdk'] + "/include/"
wwise_sdk_libs_path = ""
wwise_soundengine_sample_path = ""

if env['platform'] == "windows":
    wwise_soundengine_sample_path = env['wwise_sdk'] + "/samples/SoundEngine/Win32/"
    if env['target'] in ('debug', 'd'):
        wwise_sdk_libs_path = env['wwise_sdk'] + "/x64_vc160/Debug/lib/"
    else:
        wwise_sdk_libs_path = env['wwise_sdk'] + "/x64_vc160/Release/lib/"

if env['platform'] == "osx":
    wwise_soundengine_sample_path = env['wwise_sdk'] + "/samples/SoundEngine/POSIX/"
    if env['target'] in ('debug', 'd'):
        wwise_sdk_libs_path = env['wwise_sdk'] + "/Mac/Debug/lib/"
    else:
        wwise_sdk_libs_path = env['wwise_sdk'] + "/Mac/Release/lib/"   

if env['platform'] == "ios" and not env['ios_simulator']:
    wwise_soundengine_sample_path = env['wwise_sdk'] + "/samples/SoundEngine/POSIX/"
    if env['target'] in ('debug', 'd'):
        wwise_sdk_libs_path = env['wwise_sdk'] + "/iOS/Debug-iphoneos/lib/"
    else:
        wwise_sdk_libs_path = env['wwise_sdk'] + "/iOS/Release-iphoneos/lib/"  

if env['platform'] == "ios" and env['ios_simulator']:
    wwise_soundengine_sample_path = env['wwise_sdk'] + "/samples/SoundEngine/POSIX/"
    if env['target'] in ('debug', 'd'):
        wwise_sdk_libs_path = env['wwise_sdk'] + "/iOS/Debug-iphonesimulator/lib/"
    else:
        wwise_sdk_libs_path = env['wwise_sdk'] + "/iOS/Release-iphonesimulator/lib/"

if env['platform'] == "linux":
    wwise_soundengine_sample_path = env['wwise_sdk'] + "/samples/SoundEngine/POSIX/"
    if env['target'] in ('debug', 'd'):
        wwise_sdk_libs_path = env['wwise_sdk'] + "/Linux_x64/Debug/lib/"
    else:
        wwise_sdk_libs_path = env['wwise_sdk'] + "/Linux_x64/Release/lib/"   

wwise_soundengine_sample_common_path = env['wwise_sdk'] + "/samples/SoundEngine/Common/"
wwise_memorymanager_library = "AkMemoryMgr"
wwise_soundengine_library = "AkSoundEngine"
wwise_musicengine_library = "AkMusicEngine"
wwise_streammanager_library = "AkStreamMgr"
wwise_communications_library = "CommunicationCentral"
wwise_plugins_library = ["AkGainFX", "AkHarmonizerFX", "AkStereoDelayFX", "AkTimeStretchFX",
                         "AkTremoloFX", "AkGuitarDistortionFX","AkVorbisDecoder", "AkOpusDecoder", "AkMeterFX", 
                         "AkPitchShifterFX", "AkRoomVerbFX", "AkRoomVerbFX", "AkPeakLimiterFX", 
                         "AkExpanderFX", "AkCompressorFX", "AkMatrixReverbFX", "AkParametricEQFX", 
                         "AkDelayFX","AkSilenceSource", "AkToneSource", "AkFlangerFX", "AkSineSource",
                         "AkRecorderFX", "AkAudioInputSource", "AkSynthOneSource", "AkSpatialAudio"]

if 'reflect' in env['plugins']:
    wwise_plugins_library.append('AkReflectFX')

if 'convolution' in env['plugins']:
    wwise_plugins_library.append('AkConvolutionReverbFX')

if 'soundseed_grain' in env['plugins']:
    wwise_plugins_library.append('AkSoundSeedGrainSource')

if 'soundseed_air_impact' in env['plugins']:
    wwise_plugins_library.append(['AkSoundSeedWindSource', 'AkSoundSeedImpactFX', 'AkSoundSeedWooshSource'])

if 'impacter' in env['plugins']:
    wwise_plugins_library.append('AkImpacterSource')
    env.Append(CPPDEFINES=['AK_IMPACTER'])

if 'mastering_suite' in env['plugins']:
    wwise_plugins_library.append('MasteringSuiteFX')
    env.Append(CPPDEFINES=['AK_MASTERING_SUITE'])

print("Wwise SDK headers path: " + wwise_sdk_headers_path)
print("Wwise SDK libs path: " + wwise_sdk_libs_path)

# Process some arguments
if env['use_llvm']:
    env['CC'] = 'clang'
    env['CXX'] = 'clang++'

if env['p'] != '':
    env['platform'] = env['p']

if env['platform'] == '':
    print("No valid target platform selected.")
    quit();

# For the reference:
# - CCFLAGS are compilation flags shared between C and C++
# - CFLAGS are for C-specific compilation flags
# - CXXFLAGS are for C++-specific compilation flags
# - CPPFLAGS are for pre-processor flags
# - CPPDEFINES are for pre-processor defines
# - LINKFLAGS are for linking flags

# Check our platform specifics
if env['platform'] == "osx":
    env['target_path'] += 'osx/'
    if env['target'] in ('debug', 'd'):
        env['target_path'] += 'debug/'
    else:
        env['target_path'] += 'release/'

    cpp_library += '.osx'

    wwise_plugins_library.append("AkAACDecoder")
    
    if env['target'] in ('debug', 'd'):
        env.Append(CCFLAGS=['-g', '-O2', '-arch', 'x86_64'])
        env.Append(CXXFLAGS=['-std=c++17'])
        env.Append(LINKFLAGS=['-arch', 'x86_64', '-framework', 'CoreAudio', '-Wl,-undefined,dynamic_lookup'])
    else:
        env.Append(CPPDEFINES=['AK_OPTIMIZED'])
        env.Append(CCFLAGS=['-g', '-O3', '-arch', 'x86_64'])
        env.Append(CXXFLAGS=['-std=c++17'])
        env.Append(LINKFLAGS=['-arch', 'x86_64', '-framework', 'CoreAudio', '-Wl,-undefined,dynamic_lookup'])

    if 'reflect' in env['plugins']:
        env.Append(CPPDEFINES=['AK_REFLECT'])
    if 'convolution' in env['plugins']:
        env.Append(CPPDEFINES=['AK_CONVOLUTION'])
    if 'soundseed_grain' in env['plugins']:
        env.Append(CPPDEFINES=['AK_SOUNDSEED_GRAIN'])
    if 'soundseed_air_impact' in env['plugins']:
        env.Append(CPPDEFINES=['AK_SOUNDSEED_AIR_IMPACT'])

elif env['platform'] == 'ios':
    if env['ios_simulator']:
        env['target_path'] += 'iOS-simulator/'
        sdk_name = 'iphonesimulator'
        env.Append(CCFLAGS=['-mios-simulator-version-min=10.0'])
    else:
        env['target_path'] += 'iOS/'
        sdk_name = 'iphoneos'
        env.Append(CCFLAGS=['-miphoneos-version-min=10.0'])

    if env['target'] in ('debug', 'd'):
        env['target_path'] += 'debug/' + env['ios_arch'] + "/"
    elif env['target'] in ('release', 'r'):
        env['target_path'] += 'release/' + env['ios_arch'] + "/"
    
    cpp_library += '.ios'

    try:
        sdk_path = decode_utf8(subprocess.check_output(['xcrun', '--sdk', sdk_name, '--show-sdk-path']).strip())
    except (subprocess.CalledProcessError, OSError):
        raise ValueError("Failed to find SDK path while running xcrun --sdk {} --show-sdk-path.".format(sdk_name))

    compiler_path = env['IPHONEPATH'] + '/usr/bin/'
    env['ENV']['PATH'] = env['IPHONEPATH'] + "/Developer/usr/bin/:" + env['ENV']['PATH']

    env['CC'] = compiler_path + 'clang'
    env['CXX'] = compiler_path + 'clang++'
    env['AR'] = compiler_path + 'ar'
    env['RANLIB'] = compiler_path + 'ranlib'

    if env['target'] == 'debug':
        env.Append(CCFLAGS=['-Og', '-g', '-std=c++17', '-arch', env['ios_arch'], '-isysroot', sdk_path])
        env.Append(LINKFLAGS=['-framework', 'CoreAudio', '-framework', 'AudioToolbox', '-Wl,-undefined,dynamic_lookup', '-arch', env['ios_arch'], 
                            '-isysroot', sdk_path, '-F' + sdk_path])
    elif env['target'] == 'release':
        env.Append(CPPDEFINES=['AK_OPTIMIZED'])
        env.Append(CCFLAGS=['-O3', '-g', '-std=c++17', '-arch', env['ios_arch'], '-isysroot', sdk_path])
        env.Append(LINKFLAGS=['-framework', 'CoreAudio', '-framework', 'AudioToolbox', '-Wl,-undefined,dynamic_lookup', '-arch', env['ios_arch'], 
                             '-isysroot', sdk_path, '-F' + sdk_path])

    wwise_plugins_library.append("AkAACDecoder")
    
elif env['platform'] in ('x11', 'linux'):

    env.Prepend(_LIBFLAGS="-Wl,--start-group ")
    env.Append(_LIBFLAGS=" -Wl,--end-group")

    wwise_plugins_library.append("AkOpusDecoder")

    env.Append(LIBPATH=['/lib/x86_64-linux-gnu'])

    env['target_path'] += 'linux/'
    if env['target'] in ('debug', 'd'):
        env['target_path'] += 'debug/'
    else:
        env['target_path'] += 'release/'

    cpp_library += '.linux'

    if env['target'] in ('debug', 'd'):
        env.Append(CCFLAGS=['-Wall', '-g3', '-Og'])
        env.Append(CXXFLAGS=['-std=c++17', '-D__cdecl=""'])
    else:
        env.Append(CCFLAGS=['-g', '-O3'])
        env.Append(CXXFLAGS=['-std=c++17', '-D__cdecl=""'])
        env.Append(CPPDEFINES=['AK_OPTIMIZED'])

    if 'reflect' in env['plugins']:
        env.Append(CPPDEFINES=['AK_REFLECT'])
    if 'convolution' in env['plugins']:
        env.Append(CPPDEFINES=['AK_CONVOLUTION'])
    if 'soundseed_grain' in env['plugins']:
        env.Append(CPPDEFINES=['AK_SOUNDSEED_GRAIN'])
    if 'soundseed_air_impact' in env['plugins']:
        env.Append(CPPDEFINES=['AK_SOUNDSEED_AIR_IMPACT'])

elif env['platform'] == "windows":
    env['target_path'] += 'win64/'
    if env['target'] in ('debug', 'd'):
        env['target_path'] += 'debug/'
    else:
        env['target_path'] += 'release/'

    cpp_library += '.windows'

    if 'motion' in env['plugins']:
        wwise_plugins_library.append(['AkMotionSink', 'AkMotionSourceSource'])
    

    env.Append(CPPDEFINES=['WIN32', '_WIN32', '_WINDOWS', '_CRT_SECURE_NO_WARNINGS', 'UNICODE'])
    env.Append(CCFLAGS=['-W3', '-GR'])
    #env.Append(CCFLAGS=['/GS', '/GL', '/Gy'])

    if env['target'] in ('debug', 'd'):
        env.Append(CPPDEFINES=['_DEBUG'])
        env.Append(CCFLAGS=['-EHsc', '-MDd', '-ZI'])
        env.Append(LINKFLAGS=['-DEBUG'])
    else:
        env.Append(CPPDEFINES=['AK_OPTIMIZED'])
        env.Append(CPPDEFINES=['NDEBUG'])
        env.Append(CCFLAGS=['-O2', '-EHsc', '-MD'])

    if 'reflect' in env['plugins']:
        env.Append(CPPDEFINES=['AK_REFLECT'])
    if 'motion' in env['plugins']:
        env.Append(CPPDEFINES=['AK_MOTION'])
    if 'convolution' in env['plugins']:
        env.Append(CPPDEFINES=['AK_CONVOLUTION'])
    if 'soundseed_grain' in env['plugins']:
        env.Append(CPPDEFINES=['AK_SOUNDSEED_GRAIN'])
    if 'soundseed_air_impact' in env['plugins']:
        env.Append(CPPDEFINES=['AK_SOUNDSEED_AIR_IMPACT'])

if env['target'] in ('debug', 'd'):
    cpp_library += '.debug'
else:
    cpp_library += '.release'

if env['platform'] == 'ios':
    if env['ios_arch'] == 'arm64':
        cpp_library += '.' + 'arm64'
    elif env['ios_arch'] == 'armv7':
        cpp_library += '.' + 'armv7'
    elif env['ios_arch'] == 'x86_64':
        cpp_library += '.' + 'x86_64'

    if env['ios_simulator']:
        cpp_library += '.' + 'simulator'
else:
    cpp_library += '.' + str(bits)

# make sure our binding library is properly includes
env.Append(CPPPATH=['.', godot_headers_path, cpp_bindings_path + 'include/', cpp_bindings_path + 'include/core/', cpp_bindings_path + 'include/gen/'])
env.Append(LIBPATH=[cpp_bindings_path + 'bin/'])
env.Append(LIBS=[cpp_library])

# tweak this if you want to use different folders, or more folders, to store your source code in.
env.Append(CPPPATH=['src/'])

sources = []
sources.append(Glob('src/*.cpp')) 
sources.append(Glob(wwise_soundengine_sample_path + '*.cpp'))
sources.append(Glob(wwise_soundengine_sample_common_path + '*.cpp'))

# wwise specific configuration
env.Append(CPPPATH=[wwise_sdk_headers_path, wwise_soundengine_sample_path])
env.Append(LIBPATH=[wwise_sdk_libs_path])
env.Append(LIBS=[wwise_memorymanager_library, wwise_soundengine_library, wwise_streammanager_library, wwise_musicengine_library, wwise_plugins_library])

if env['target'] in ('debug', 'd'):
    env.Append(LIBS=[wwise_communications_library])

    if env['platform'] == "windows":
    	env.Append(LIBS=['msacm32', 'ws2_32'])

if env['platform'] == "windows":
    env.Append(LIBS=['advapi32', 'user32', 'ole32'])
if env['platform'] == "windows" and 'motion' in env['plugins']:
    env.Append(LIBS=['XInput', 'Dinput8', 'Winmm', 'oleaut32'])
if env["platform"] == "linux":
    env.Append(LIBS=[ 'pthread', 'dl'])

library = env.SharedLibrary(target=env['target_path'] + env['target_name'] , source=sources)

Default(library)

# Generates help for the -h scons option.
Help(opts.GenerateHelpText(env))
